apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

jacocoTestReport {

  reports {
    xml.enabled true
    html.enabled true
  }

  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: [
        '**/opencensus/**',
        '**/SerdeProducer.*',
        "**/SchemaRegistryClientProducer.*",
        "**/ReadOnlyKeyValueStoreProducer.*"
      ])
    }))
  }
}

jacocoTestReport.dependsOn test

pmd {
  // Empty ruleset is necessary for exclude-pattern
  // https://stackoverflow.com/questions/32247190/pmd-exclude-pattern-with-gradle
  ruleSets = []
  ruleSetFiles = files("code-analysis/pmd.xml")
  ignoreFailures = false
  toolVersion = "6.41.0"
}

checkstyle {
  configDirectory = file("code-analysis")
  configFile = file("code-analysis/checkstyle.xml")
  maxWarnings = 0
  ignoreFailures = false
  toolVersion = "8.12"
}

task checkstyle {
  group 'Quality Assurance'
  description 'Run Checkstyle'

  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
}

task pmd {
  group 'Quality Assurance'
  description 'Run PMD'

  dependsOn 'pmdMain'
  dependsOn 'pmdTest'
}
